import groovy.xml.MarkupBuilder

apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

def twaManifest = [
        applicationId: 'app.shrike.snowclip',
        hostName: 'snowclip-app.web.app', // The domain being opened in the TWA.
        launchUrl: '/', // The start path for the TWA. Must be relative to the domain.
        name: 'Snowclip', // The name shown on the Android Launcher.
        themeColor: '#303F9F', // The color used for the status bar.
        backgroundColor: '#bababa', // The color used for the splash screen background.
        enableNotifications: false, // Set to true to enable notification delegation.
        useBrowserOnChromeOS: false, // Set to false if you've added interaction with Android system APIs.
        // Add shortcuts for your app here. Every shortcut must include the following fields:
        // - name: String that will show up in the shortcut.
        // - short_name: Shorter string used if |name| is too long.
        // - url: Absolute path of the URL to launch the app with (e.g '/create').
        // - icon: Name of the resource in the drawable folder to use as an icon.
        shortcuts: [
                // Insert shortcuts here, for example:
                // [name: 'Open SVG', short_name: 'Open', url: '/open', icon: 'splash']
        ]
]

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.2"
    defaultConfig {
        applicationId twaManifest.applicationId
        minSdkVersion 16
        targetSdkVersion 29
        versionCode 1
        versionName "0.0.1"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        // The name for the application on the Android Launcher
        resValue "string", "appName", twaManifest.name

        // The URL that will be used when launching the TWA from the Android Launcher
        resValue "string", "launchUrl", "https://" + twaManifest.hostName + twaManifest.launchUrl

        // The hostname is used when building the intent-filter, so the TWA is able to
        // handle Intents to open https://svgomg.firebaseapp.com.
        resValue "string", "hostName", twaManifest.hostName

        // This variable below expresses the relationship between the app and the site,
        // as documented in the TWA documentation at
        // https://developers.google.com/web/updates/2017/10/using-twa#set_up_digital_asset_links_in_an_android_app
        // and is injected into the AndroidManifest.xml
        resValue "string", "assetStatements", """
            [{
                "relation": ["delegate_permission/common.handle_all_urls"],
                "target": {
                    "namespace": "web",
                    "site": "https://$twaManifest.hostName"
                }
            }]"""

        // This attribute sets the status bar color for the TWA. It can be either set here or in
        // `res/values/colors.xml`. Setting in both places is an error and the app will not
        // compile. If not set, the status bar color defaults to #FFFFFF - white.
//        resValue "color", "colorPrimary", twaManifest.themeColor

        // Sets the color for the background used for the splash screen when launching the
        // Trusted Web Activity.
        resValue "color", "backgroundColor", twaManifest.backgroundColor

        // Defines a provider authority fot the Splash Screen
        resValue "string", "providerAuthority", twaManifest.applicationId + '.fileprovider'

        // The enableNotification resource is used to enable or disable the
        // TrustedWebActivityService, by changing the android:enabled and android:exported
        // attributes
        resValue "bool", "enableNotification", twaManifest.enableNotifications.toString()

        // The useBrowserOnChromeOS resources is used to enable or disable ChromeOS support for this
        // TWA. When enabled, it causes the web app to be installed as a Desktop PWA on ChromeOS
        // but does not provide any native Android interaction.
        resValue "bool", "useBrowserOnChromeOS", twaManifest.useBrowserOnChromeOS.toString()

        twaManifest.shortcuts.eachWithIndex { shortcut, index ->
            resValue "string", "shortcut_name_$index", "$shortcut.name"
            resValue "string", "shortcut_short_name_$index", "$shortcut.short_name"
        }
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

task generateShorcutsFile {
    assert twaManifest.shortcuts.size() < 5, "You can have at most 4 shortcuts."
    twaManifest.shortcuts.eachWithIndex { s, i ->
        assert s.name != null, 'Missing `name` in shortcut #' + i
        assert s.short_name != null, 'Missing `short_name` in shortcut #' + i
        assert s.url != null, 'Missing `icon` in shortcut #' + i
        assert s.icon != null, 'Missing `url` in shortcut #' + i
    }

    def shortcutsFile = new File("$projectDir/src/main/res/xml", "shortcuts.xml")

    def xmlWriter = new StringWriter()
    def xmlMarkup = new MarkupBuilder(new IndentPrinter(xmlWriter, "    ", true))

    xmlMarkup
            .'shortcuts'('xmlns:android': 'http://schemas.android.com/apk/res/android') {
                twaManifest.shortcuts.eachWithIndex { s, i ->
                    'shortcut'(
                            'android:shortcutId': 'shortcut' + i,
                            'android:enabled': 'true',
                            'android:icon': '@drawable/' + s.icon,
                            'android:shortcutShortLabel': '@string/shortcut_short_name_' + i,
                            'android:shortcutLongLabel': '@string/shortcut_name_' + i) {
                        'intent'(
                                'android:action': 'android.intent.action.MAIN',
                                'android:targetPackage': twaManifest.applicationId,
                                'android:targetClass': 'android.support.customtabs.trusted.LauncherActivity',
                                'android:data': 'https://' + twaManifest.hostName + s.url)
                        'categories'('android:name': 'android.intent.category.LAUNCHER')
                    }
                }
            }
    shortcutsFile.text = xmlWriter.toString() + '\n'
}

preBuild.dependsOn(generateShorcutsFile)

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
//    implementation 'com.github.GoogleChrome.custom-tabs-client:customtabs:809a55cfa2'
    implementation"org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.core:core-ktx:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'

    implementation 'androidx.browser:browser:1.0.0'
    implementation 'com.github.GoogleChrome:android-browser-helper:ff8dfc4ed3d4133aacc837673c88d090d3628ec8'

    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
}
